grammar cz.cvut.earlgrey.statemodel.Statemodel with org.eclipse.xtext.common.Terminals

import "http://www.cvut.cz/earlgrey/classmodel/Classmodel" as cls
generate statemodel "http://www.cvut.cz/earlgrey/statemodel/Statemodel"

/**
 * Defines root node of a model.
 * Resource imports must be defined before other elements.
 */
Statemodel:
    (imports+=Import)* (statemachine+=Statemachine)*;

//TODO: STRING | ID |  [cls::Entity|QualifiedName]
Statemachine:
    'statemachine' reference=[cls::Entity|QualifiedName]
    '{'
    (state+=State)*
    '}';

State:
    type=StateType name=ID
    '{'
    (transitions+=Transition)*
    '}';

Transition:
    event=Event '=>' state=[State];

Event:
    name=ID;

Import:
    'import' importURI=STRING;

//TODO: remove NONE? causes "NONE state BB {}"
enum StateType:
    NONE='state' | INITIAL='initial' | FINAL='final';

QualifiedName:
    ID ('.' ID)*;

//terminal COND    : '(' (!')')* ')'; //wrong (a && (b+4))
